define AwsEFS as native category with bindings:
	
	define category bindings as:
		Java: prompto.aws.EFS
	
and methods:
	
	define listFileSystems as method returning Document[] doing:
		Java: return this.listFileSystems();
		
	define createFileSystem as method receiving Text name, Text zoneName = Nothing, Boolean backup = nothing and Boolean waitForAvailability = true returning Text doing:
		Java: return this.createFileSystem(name, zoneName, backup, waitForAvailability);
		
	define destroyFileSystem as method receiving Text fileSystemId and Boolean waitForDestruction = true doing:
		Java: this.destroyFileSystem(fileSystemId, waitForDestruction);

	define listMountTargets as method receiving Text fileSystemId returning Document[] doing:
		Java: return this.listMountTargets(fileSystemId);
		
	define createMountTarget as method receiving Text fileSystemId, Text subnetId, Text[] securityGroups = nothing and Boolean waitForAvailability = true returning Text doing:
		Java: return this.createMountTarget(fileSystemId, subnetId, securityGroups, waitForAvailability);

	define destroyMountTarget as method receiving Text mountTargetId and Boolean waitForDestruction = true doing:
		Java: return this.destroyMountTarget(mountTargetId, waitForDestruction);
		


define newAwsEFS as native method receiving Text awsRegion, login = Nothing and password = Nothing returning AwsEFS doing:
	Java: prompto.aws.EFS.newInstance(awsRegion, login, password);


define newAwsEFSWithLocalCredentials as method receiving Text awsRegion returning AwsEFS doing:
	text = read all from Url with "file:/Users/ericvergnaud/Documents/Technical/Certificates/prompto-keys/aws/us-east-1/keys.json" as path
	keys = (readJson with text as text) as Document
	login = keys.accessKey as Text
	password = keys.secretKey as Text
	return newAwsEFS with awsRegion as awsRegion, login as login and password as password


define "EFS client is created" as test method doing:
	efs = newAwsEFSWithLocalCredentials with "us-east-1" as awsRegion
and verifying:
	efs is not Nothing


define "File systems are listed" as test method doing:
	efs = newAwsEFSWithLocalCredentials with "us-east-1" as awsRegion
	fs = efs.listFileSystems
and verifying:
	fs.count >= 0
	

define "File system is created and destroyed" as test method doing:
	efs = newAwsEFSWithLocalCredentials with "us-east-1" as awsRegion
	before = efs.listFileSystems
	fsid = efs.createFileSystem "test-efs"
	after = efs.listFileSystems
	efs.destroyFileSystem fsid
	after2 = efs.listFileSystems
and verifying:
	after.count = before.count + 1
	after2.count = before.count
	

define "Mount target is created and destroyed" as test method doing:
	efs = newAwsEFSWithLocalCredentials with "us-east-1" as awsRegion
	fsid = efs.createFileSystem "test-efs"
	mtid = efs.createMountTarget with fsid as fileSystemId and "subnet-f9e252c5" as subnetId
	efs.destroyMountTarget with mtid as mountTargetId
	efs.destroyFileSystem with fsid as fileSystemId
and verifying:
	mtid is not nothing
 	
