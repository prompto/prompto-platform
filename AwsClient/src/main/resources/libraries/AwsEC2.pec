define AwsEC2 as native category with bindings:
	
	define category bindings as:
		Java: prompto.aws.EC2
	
and methods:
	
	define listInstances as method returning Document[] doing:
		Java: return this.listInstances();
		
	define runInstance as method receiving Text imageId, Text instanceType, Text keyName, Text roleName, Text[] securityGroupNames and Text userData returning Text doing:
		Java: return this.runInstance(imageId, instanceType, keyName, roleName, securityGroupNames, userData);
		
	define setInstanceName as method receiving Text instanceId and name doing:
		Java: this.setInstanceName(instanceId, name);

	define startInstance as method receiving Text instanceId doing:
		Java: this.startInstance(instanceId);

	define stopInstance as method receiving Text instanceId doing:
		Java: this.stopInstance(instanceId);

	define dropInstance as method receiving Text instanceId doing:
		Java: this.dropInstance(instanceId);
	
	define listIpAddresses as method returning Document[] doing:
		Java: return this.listIpAddresses();

	define createIpAddress as method returning Document doing:
		Java: return this.createIpAddress();

	define setIpAddressName as method receiving Text addressId and Text name doing:
		Java: this.setIpAddressName(addressId, name);

	define getAddressIdForIpAddress	as method receiving Text ipAddress returning Text doing:
		Java: return this.getAddressIdForIpAddress(ipAddress);

	define associateIPAddress as method receiving Text instanceId and Text addressId returning Text doing:
		Java: return this.associateIPAddress(instanceId, addressId);

	define dissociateIPAddress as method receiving Text addressId doing:
		Java: this.dissociateIPAddress(addressId);

	define dropIPAddress as method receiving Text addressId doing:
		Java: this.dropIPAddress(addressId);

	define listOwnedAMIs as method returning Document[] doing:
		Java: this.listOwnedAMIs();

	define createAMI as method receiving Text instanceId, Text name and Boolean waitForAvailability returning Text doing:
		Java: this.createAMI(instanceId, name, waitForAvailability);

define newAwsEC2 as native method receiving Text awsRegion, login = Nothing and password = Nothing returning AwsEC2 doing:
	Java: prompto.aws.EC2.newInstance(awsRegion, login, password);

define newAwsEC2WithLocalCredentials as method receiving Text awsRegion returning AwsEC2 doing:
	text = read all from Url with "file:/Users/ericvergnaud/Development/prompto/prompto-keys/aws/keys.json" as path
	keys = (readJson with text as text) as Document
	login = keys.accessKey as Text
	password = keys.secretKey as Text
	return newAwsEC2 with awsRegion as awsRegion, login as login and password as password

define "ec2 client can be created" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
and verifying:
	ec2 is not Nothing

define "ec2 instances can be listed" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	docs = ec2.listInstances
	names = doc["Name"] as Text for each doc in docs
and verifying:
	"prompto-deploy-021" in names

define "ec2 instances can be filtered" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	docs = ec2.listInstances filtered with doc where "prompto-deploy-" in doc.Name as Text
	names = doc["Name"] as Text for each doc in docs
and verifying:
	"prompto-deploy-021" in names


define "ec2 instance can be created, named and dropped" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	nodeId = ec2.runInstance with "ami-d2c924b2" as imageId, "t2.micro" as instanceType,
		"prompto-admin" as keyName, "deployer" as roleName, ["default"] as securityGroupNames and "some data" as userData
	ec2.setInstanceName with nodeId as instanceId and "TEST-INSTANCE" as name
	ec2.dropInstance nodeId
and verifying:
	nodeId is not Nothing
	
define "ec2 addresses can be listed" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	docs = ec2.listIpAddresses
	ips = doc["publicIp"] as Text for each doc in docs
and verifying:
	"35.166.112.187" in ips

define "ec2 address can be found by ip" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	name = ec2.getAddressIdForIpAddress "35.166.112.187"
and verifying:
	"eipalloc-dc98c1bb" = name

define "ec2 address can be created, named, associated, dissociated and dropped" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	doc = ec2.createIpAddress
	addressId = doc.allocationId as Text
	ec2.setIpAddressName with addressId as addressId and "TEST-ADDRESS" as name
	assocId = ec2.associateIPAddress with "i-0dbd77527d5da6b49" as instanceId and addressId as addressId
	ec2.dissociateIPAddress assocId
	ec2.dropIPAddress addressId
and verifying:
	addressId is not Nothing
	assocId is not Nothing

define "owned AMIs can be listed" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	amis = ec2.listOwnedAMIs
and verifying:
	amis is not Nothing
	amis.count > 0
	(amis[1]["Name"] as Text).startsWith "centos-prompto"


define getAMIVersion as method receiving Document value returning Version doing:
	name = value["Name"] as Text
	parts = name.split "\\-"
	return parseVersion parts[3]


define "AMI version is extracted" as test method doing:
	ami = { Name: "centos-prompto-v0.0.22" }
	version = getAMIVersion with ami as value
and verifying:
	version = 'v0.0.22'
		
		
define "latest AMI is identified" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	amis = ec2.listOwnedAMIs
	amis = sorted descending amis with getAMIVersion as key
	ami = amis[1]
and verifying:
	ami["Name"] = "centos-prompto-v0.0.24"
	ami["imageId"] = "ami-02770ccc3a39f33c1"
	

define "AMI can be created" as test method doing:
	ec2 = newAwsEC2WithLocalCredentials with "us-east-1" as awsRegion
	amiId = ec2.createAMI with "i-0295d2ed128657bff" as instanceId, "test-ami" as name and true as waitForAvailability
and verifying:
	amiId is not Nothing
	
	
	