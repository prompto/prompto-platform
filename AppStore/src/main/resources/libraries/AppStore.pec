define AuthenticationMethod as storable category
define authenticationMethod as storable AuthenticationMethod attribute

define NoAuthenticationMethod as storable AuthenticationMethod

define BasicAuthenticationMethod as storable AuthenticationMethod

define loginPage as storable Text attribute
define errorPage as storable Text attribute
define FormAuthenticationMethod as storable AuthenticationMethod with attributes loginPage and errorPage

define AuthenticationSource as storable category
define authenticationSource as storable AuthenticationSource attribute

define storeName as storable Text attribute
define DataStoreAuthenticationSource as storable AuthenticationSource with attribute storeName

define PasswordIsLoginAuthenticationSource as storable AuthenticationSource

define skipAuthInDev as storable Boolean attribute
define useTestSourceInDev as storable Boolean attribute
define whiteList as storable Text[] attribute
define useDefaultWhiteList as storable Boolean attribute

define AuthenticationSettings as storable category with attributes authenticationMethod, skipAuthInDev, authenticationSource, useTestSourceInDev, whiteList and useDefaultWhiteList  
define authenticationSettings as storable AuthenticationSettings attribute

// don't directly connect Modules to keep them lightweight
// also eliminates any (accidental) cyclical dependency
define Dependency as storable category with attributes name and version
define dependencies as storable Dependency[] attribute

define Module as storable category with attributes name, version, description, image and dependencies

define Library as storable Module
define Thesaurus as storable Library

define Application as storable Module
define Script as storable Application

define startMethod as storable Text attribute
define Batch as storable Application with attribute startMethod


define serverAboutToStartMethod as storable Text attribute
define Service as storable Module with attributes serverAboutToStartMethod and authenticationSettings

define homePage as storable Text attribute
define WebSite as storable Service and Application with attribute homePage 

define widgetLibrary as storable Text attribute
define htmlEngine as storable Text attribute
define uiFramework as storable Text attribute
define WebLibrary as storable Library with attributes widgetLibrary, htmlEngine and uiFramework

define ReleaseStatus as enumerated Text with symbols:
    PENDING with "Pending" as value
    CANCELLED with "Cancelled" as value
    RELEASED with "Released" as value
    ARCHIVED with "Archived" as value

define module as storable Module attribute
define supersedesOlder as storable Boolean attribute
define releaseStatus as storable ReleaseStatus attribute with key index
// not a member of Application because a given release might not succeed, so we might have multiple releases per application
define Release as storable category with attributes module, supersedesOlder, author, timeStamp and releaseStatus

define fetchReleases as method receiving releaseStatus doing:
	return fetch all Release where releaseStatus = releaseStatus

define fetchUnreleased as method doing:
	apps = fetch all Application
	return apps filtered with app where (fetch one Release where module = app and releaseStatus in [PENDING, RELEASED] ) is nothing

define startRelease as method receiving dbId, supersedesOlder and Text[] signOffUsers doing:
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	author = getHttpUser
	timeStamp = now
	release = Release with module as module, supersedesOlder as supersedesOlder, author as author, timeStamp as timeStamp and PENDING as releaseStatus
	store release

define setReleaseStatus as method receiving dbId, releaseStatus doing:
	release = fetch one mutable Release where dbId = dbId
	if release is nothing:
		raise NULL_REFERENCE
	release.releaseStatus = releaseStatus
	store release

define coresCount as storable Integer attribute
define memoryMegabytes as storable Integer attribute
define storageGigabytes as storable Integer attribute

define DataCenter as storable category with attribute name 
define dataCenter as storable DataCenter attribute
define dataCenters as storable DataCenter[] attribute

define NodeSize as storable category with attributes coresCount, memoryMegabytes and storageGigabytes
define nodeSize as storable NodeSize attribute
define nodeCount as storable Integer attribute

define release as storable Release attribute
define Deployment as storable category with attributes release, url, nodeSize, nodeCount, dataCenters, author and timeStamp

define fetchDeployed as method doing:
	return fetch all Deployment

define fetchUndeployed as method doing:
	releases = fetchReleases with RELEASED as releaseStatus
	return releases filtered with release where (fetch one Deployment where release = release) is nothing

define deployRelease as method receiving dbId, nodeSize, nodeCount and dataCenters doing:
	release = fetch one Release where dbId = dbId
	if release is nothing:
		raise NULL_REFERENCE
	author = getHttpUser
	timeStamp = now
	deployment = Deployment with release as release, nodeSize as nodeSize, nodeCount as nodeCount, dataCenters as dataCenters, author as author and timeStamp as timeStamp
	store deployment
define createLibraries as native method doing:
	Java: prompto.codefactory.Application.createLibraries();

define serverAboutToStart as method receiving Text<:> params doing:
	createLibraries

define lastOpened as storable DateTime attribute
define CodingSession as storable category with attributes login, module and lastOpened
define RecentSession as storable CodingSession

define Stuff as storable category with attributes name, version and module

define storable as storable Boolean attribute
define Declaration as storable Stuff with attributes dialect, storable and body 
define AttributeDeclaration as storable Declaration
define CategoryDeclaration as storable Declaration
define WidgetDeclaration as storable CategoryDeclaration
define symbols as storable Text[] attribute
define EnumeratedDeclaration as storable Declaration with attribute symbols
define MethodDeclaration as storable Declaration with attribute prototype
define TestDeclaration as storable Declaration

define Resource as storable Stuff with attribute mimeType
define TextResource as storable Resource with attribute body
define BinaryResource as storable Resource with attribute data

define EditStatus as enumerated Text with symbols:
	CLEAN with "CLEAN" as value
	CREATED with "CREATED" as value
	DELETED with "DELETED" as value 
	DIRTY with "DIRTY" as value

define editStatus as EditStatus attribute
define stuff as Stuff attribute

define EditedStuff as category with attributes editStatus and stuff

define getRecentModules as method receiving Integer count doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	sessions = fetch RecentSession 1 to count where login = login order by lastOpened desc
	return session.module for each session in sessions

define getAllModules as method doing:
	return fetch all Module order by name


define getAllLibraries as method doing:
	return fetch all Library order by name, version desc


define getModuleDescription as method receiving dbId and Boolean register doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	if register:
		// use this opportunity to keep track of coding sessions
		session = CodingSession with login, module and now as lastOpened 
		store session
		// use this opportunity to keep track of recent projects
		recent = fetch one mutable RecentSession where login = login and module = module
		if recent is nothing:
			recent = RecentSession with login, module and now as lastOpened 
		else:
			recent.lastOpened = now 
		store recent
		flush
	return module


define getModuleDeclarations as method receiving name and version doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	module = fetch one Module where name = name and version = version
	if module is nothing:
		raise NULL_REFERENCE
	return fetch all Declaration where module = module


define getModuleDeclarations as method receiving dbId doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	return fetch all Declaration where module = module


define getModuleResources as method receiving name and version doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	module = fetch one Module where name = name and version = version
	if module is nothing:
		raise NULL_REFERENCE
	return fetch all Resource where module = module


define getModuleResources as method receiving dbId doing:
	login = getHttpUser
	if login is nothing:
		raise NULL_REFERENCE
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	return fetch all Resource where module = module

define getModulePort as native method receiving dbId and Boolean optional returning Integer doing:
	Java: return prompto.codefactory.ModuleProcess.launchIfNeeded(dbId, optional);

define storeEdited as method receiving EditedStuff[] edited doing:
	deleted = d1.stuff for each d1 in edited filtered with d where d.editStatus = DELETED
	created = d2.stuff for each d2 in edited filtered with d where d.editStatus = CREATED
	updated = d3.stuff for each d3 in edited filtered with d where d.editStatus = DIRTY
	delete deleted and store created, updated 
	flush


define createScript as method receiving name, description = nothing doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = Script with name, version, description and dependencies
	store module
	flush


define createLibrary as method receiving name, description = nothing doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = Library with name, version, description and dependencies
	store module
	flush


define createWebLibrary as method receiving name, description = nothing, widgetLibrary, htmlEngine and uiFramework doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = WebLibrary with name, version, description, widgetLibrary, htmlEngine, uiFramework and dependencies
	store module
	flush


define createBatch as method receiving name, description = nothing and startMethod doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = Batch with name, version, description, startMethod and dependencies
	decl = createStartMethod with module and startMethod as name
	store decl, module
	flush


define createService as method receiving name, description = nothing and serverAboutToStartMethod doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = Service with name, version, description, serverAboutToStartMethod and dependencies
	decl = createStartMethod with module and serverAboutToStartMethod as name
	store decl, module
	flush


define createWebSite as method receiving name, description = nothing, image = nothing, serverAboutToStartMethod and homePage doing:
	// at minimal, all modules depend on Thesaurus
	lib = fetch one Library where name = "Thesaurus"
	dependencies = [Dependency from lib]
	version = 'v0.0.1'
	module = WebSite with name, version, description, image, serverAboutToStartMethod, homePage and dependencies
	decl = createStartMethod with module and serverAboutToStartMethod as name
	resources = createHomeHtmlOrPageResources with module and homePage as name
	store decl, resources, module
	flush


define createStartMethod as method receiving module and name doing:
	body = "define " + name + " as method receiving Text<:> options doing:\n"
	body = body + "\tprint \"Hello " + module.name + "\"\n" 
	decl = MethodDeclaration with name, 'v0.0.1' as version, "E" as dialect, "Text<:>" as prototype, body and module
	return decl 


define createHomeHtmlOrPageResources as method receiving module and name doing:
	if name.endsWith ".page":
		return createHomePageResources with module and name
	else:
		return createHomeHtmlResources with module and name

		
define createHomeHtmlResources as method receiving module and name doing:
	body = "<!DOCTYPE html>\n<html>\n\t<head>\n\t</head>\n\t<body>\n\t\tHello!\n\t</body>\n</html>"
	page = TextResource with name, 'v0.0.1' as version, "text/html" as mimeType, body and module
	return [page]


define createHomePageResources as method receiving module and name doing:
	start = name.indexOf "/"
	widgetName = name[start + 1:-6].toCapitalized + "Page"
	body = "@PageWidgetOf(\"" + name + "\")\n"
	body = body + "widget " + widgetName + " {\n\n"
	body = body + "\tHtml method render() {\n"
	body = body + "\t\treturn <div>Hello \"" + module.name + "\"!</div>;\n"
	body = body + "\t}\n\n}\n" 
	decl = WidgetDeclaration with widgetName as name, 'v0.0.1' as version, "O" as dialect, body and module
	body = "header:\n"
	body = body + "  title: " + module.name + "\n"
	body = body + "  icon: favicon.ico\n"
	body = body + "  widgetLibrary: react-bootstrap-3\n"
	body = body + "body:\n"
	body = body + "  widget: " + widgetName + "\n"
	page = TextResource with name, 'v0.0.1' as version, "text/page" as mimeType, body and module
	return [decl, page]


define storeModule as method receiving module doing:
	store module
	flush

define deleteModule as method receiving dbId doing:
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	stuff = fetch all Stuff where module = module
	sessions = fetch all RecentSession where module = module
	delete stuff, sessions, module, module.dependencies
	flush


define getModuleTypeName as method receiving module doing:
	// TODO use reflection once available
	if module is a Batch:
		return "Batch"
	else if module is a Script:
		return "Script"
	else if module is a WebSite:
		return "WebSite"
	else if module is a Service:
		return "Service"
	else if module is a Library:
		return "Library"
	else if module is a WebLibrary:
		return "WebLibrary"
	else:
		return "<unknown>"

define exportModule as method receiving dbId doing:
	module = fetch one Module where dbId = dbId
	if module is nothing:
		raise NULL_REFERENCE
	doc = Document
	doc.type = getModuleTypeName module
	doc.name = module.name
	doc.version = module.version
	doc.description = module.description
	doc.image = module.image
	if module is a Batch:
		doc.startMethod = module.startMethod
	else if module is a Service:
		doc.serverAboutToStartMethod = module.serverAboutToStartMethod
	if module is a WebSite:
		doc.homePage = module.homePage
	if module is a WebLibrary:
		doc.widgetLibrary = module.widgetLibrary
		doc.htmlEngine = module.htmlEngine
		doc.uiFramework = module.uiFramework
	components = [] as Stuff[]
	fetched = fetch all mutable Stuff where module = module
	// don't re-export module itself
	for each component in fetched:
		component.module = Nothing 
		components = components + [component]
	doc.components = components
	return Blob from doc
